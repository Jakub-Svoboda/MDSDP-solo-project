grammar dk.sdu.mdsd.guilang.Guilang with org.eclipse.xtext.common.Terminals

generate guilang "http://www.sdu.dk/mdsd/guilang/Guilang"

GUI:
	vars+=Variable*
	main=Main
	templates+=Template*
;

Main returns Unit:
	'main' Unit
;

Unit:
	OPEN
		layout=Layout
		specifications=Specifications?
	CLOSE
;

Template:
	'template' name=ID unit=Unit
;

Entity:
	Layout | Element
;

Layout:
	(
		{Vertical} key=('vertical' | 'vert') | 
		{Horizontal} key=('horizontal' | 'hori')
	) name=ID? OPEN entities+=Entity* CLOSE
;

Element: (
		{Label} key=('lbl' | 'label') |
		{Button} key=('btn' | 'button') | 
		{Input} key=('field' | 'input') | 
		{Checkbox} key=('chk' | 'checkbox') |
		{TextArea} key=('text-area') |
		{TemplateInstance} key='new' ref=[Template] |
		{List} key='list' ref=[Template]
	) name=ID ('[' inlineOptions+=Option (';' inlineOptions+=Option)* ']')?
;

Specifications:
	{Specifications} 'elements' OPEN
		specifications+=Specification*
	CLOSE
;

Specification:
	ref=[Entity] OPEN options+=Option* CLOSE
;

Option:
	{DimOption} (key='dim' | key='dimensions') (width=INT'x'height=INT | ('w'| 'width') width=INT  ('h'| 'height') height=INT) |
	{SizeOption} key='size' value=STRING |
    {BackgroundColor} key='bg-color' (color=STRING | var=[ColorVariable]) |
	{TextColor} key='text-color' (color=STRING | var=[ColorVariable]) |
	{TextSize} key='text-size' value=INT unit=('px' | '%') |
    {TextValue} key='text' STRING |
    {TextValidate} key='validate' (value=RegexValidate | var=[RegexVariable] | preset=PresetTextValidate) |
    {CheckboxValidate} key='validate' (value='is-checked' | value='is-unchecked') |
    {RequireElements} key='require' req=Require
;

Variable:
	{ColorVariable} 'color' name=ID '=' value=STRING |
    {RegexVariable} 'regex' name=ID '=' value=STRING
;

RegexValidate:
	 'regex' '=' STRING
;

enum PresetTextValidate:
	password |
	email |
	not_empty
	// etc...
;

Require:
	RequireOr
;

RequireOr returns Require:
	RequireAnd ({Or.left=current} ('||') right=RequireAnd)*
;

RequireAnd returns Require:
	RequireBase ({And.left=current} ('&&') right=RequireBase)*
;

RequireBase returns Require:
	'(' exp=Require ')' | 
	{Not} ('!' | 'NOT') exp=RequireBase | 
	{ElementRef} ref=[Element]
;

/*
RequireEquality returns Require:
	
;

RequireComparison returns Require:
	
;*/


// ##################################################################
// ## Terminals
// ##################################################################

terminal OPEN:
	'{'
;

terminal CLOSE:
	'}'
;
